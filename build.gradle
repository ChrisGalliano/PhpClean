plugins {
    id 'org.jetbrains.intellij' version '0.3.5'
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
}
repositories {
    mavenCentral()
    maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
if (file('local.properties').exists()) {
    apply from: 'local.properties'
}
patchPluginXml {
    changeNotes project.property("changeNotes").toString().replace("\n", "<br>\n")
}
intellij {
    version ideaVersion
    sandboxDirectory project.rootDir.canonicalPath + "/build/idea-sandbox"
    downloadSources Boolean.valueOf(downloadIdeaSources)
    updateSinceUntilBuild = false
    pluginName name
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'CSS',
            'java-i18n',
            'PsiViewer:3.28.93',
            'properties'
    ]
}
jar.archiveName = "${name}.jar"

task patchRepositoryXml(type: Copy) {
    from 'src/ci/PhpClean-nightly.xml'
    into "${buildDir}/libs"
    expand([
            buildDate : System.currentTimeMillis(),
            fileSize  : '18000',
            version   : project.property("version").toString(),
            pluginName: project.property("pluginName"),
            fileName  : jar.archiveName,
            group     : project.property("group")
    ])
    doFirst {
        ant.defaultexcludes remove: "${buildDir}/libs/*.xml"
    }
}
task deployNightly(type: Exec) {
    def ci_deploy_uri = project.hasProperty("ci_deploy_uri") ? project.property("ci_deploy_uri").toString() : System.getenv("DEPLOY_URI")
    commandLine "curl", "-F", "file[]=@build/libs/PhpClean.jar", "-F", "file[]=@build/libs/PhpClean-nightly.xml", "${ci_deploy_uri}"
}
apply plugin: "idea"
apply plugin: "kotlin"
apply plugin: "org.jetbrains.intellij"